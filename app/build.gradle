apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext {
    versions = [
            major: 1,
            minor: 0
    ]
    roomVersion = "1.1.1"
    kodeinVersion = "5.1.0"
    supportLibraryVersion = "28.0.0"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.nanlagger.packinglist"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode codeVersion()
        versionName "${versions.major}.${versions.minor}-alpha (${codeVersion()})"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"

    //Cicerone Navigation
    implementation "ru.terrakok.cicerone:cicerone:2.1.0"

    //Log
    implementation "com.jakewharton.timber:timber:4.5.1"

    //Room
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"

    //RxJava
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "io.reactivex.rxjava2:rxjava:2.1.3"

    //Permissions
    implementation 'com.karumi:dexter:4.2.0'

    //Kodein
    implementation "org.kodein.di:kodein-di-generic-jvm:$kodeinVersion"
    implementation "org.kodein.di:kodein-di-framework-android:$kodeinVersion"

    //Adapter simplify
    implementation 'com.hannesdorfmann:adapterdelegates3:3.0.1'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

static def codeVersion() {
    return Integer.valueOf(System.env.BUILD_NUMBER ?: 0)
}

///////////////////////////////////////////////////
////////////////// Change apk name ////////////////
///////////////////////////////////////////////////
android.applicationVariants.all { variant ->
    for (output in variant.outputs) {
        def name = output.outputFileName
        if (name != null && name.endsWith('.apk')) {
            output.outputFileName = "${variant.buildType.name}-${versionCode}.apk"
        }
    }
}